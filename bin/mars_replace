#!/bin/sh
# -*-tcl-*-
# The next line restarts using tclsh \
exec tclsh8.5 "$0" "$@"

#-----------------------------------------------------------------------
# TITLE:
#	mars_replace
#
# AUTHOR:
#	Will Duquette
#
# DESCRIPTION:
#       This program is a bulk search-and-replace tool.  Given a pair
#       of strings, one to search for and one to replace it with, and
#       a list of files, it will replace all occurrences of the target
#       with the replacement in each of the files.  The original file
#       is renamed "<name>~".
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Required Packages

# None

#-----------------------------------------------------------------------
# Main Routine

# main argv
#
# argv     The command line arguments
#
# Processes the files on the command-line, replacing the search target
# with the replacement text.

proc main {argv} {
    # FIRST, process the command line.
    if {[llength $argv] < 3} {
        ShowUsage
        exit
    }

    set target [lindex $argv 0]
    set replacement [lindex $argv 1]
    set files [lrange $argv 2 end]

    # NEXT, Step over the files
    foreach file $files {
        if {[catch {ReplaceString $target $replacement $file} result]} {
            puts "-- Error, $result"
        }
    }
}

proc ReplaceString {target replacement file} {
    # FIRST, read the text from the file
    set f [open $file r]
    set text [read $f]
    close $f

    # NEXT, see whether it contains the string at all.  If not, skip this
    # one.
    if {[string first $target $text] == -1} {
        return
    }

    puts "mars_replace: $file"

    # NEXT, backup the file
    set backup "$file~"
    file copy -force $file $backup

    # NEXT, do the replacement.
    set text [string map [list $target $replacement] $text]

    # NEXT, save the new text.
    set f [open $file w]
    puts $f $text
    close $f
}

#-----------------------------------------------------------------------
# Utility Commands

# ShowUsage
#
# Display the usage syntax for the command.

proc ShowUsage {} {
    puts "Usage: mars_replace target replacement files..."
}




#-----------------------------------------------------------------------
# Run the program

main $argv








